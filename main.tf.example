# Probably the most useful variable, enter
# the domain where you want your website hosted
variable "domain_website" {
  default = ""
}
# Visit https://console.aws.amazon.com/route53/home#hosted-zones:
# and look at the "Hosted Zone ID" column
variable "route53_zone_id" {
  default = ""
}
# A number of entities are global and some are tied to us-east-1
# but this allows you to choose to place your S3 buckets in a
# region of your choosing
variable "aws_region" {
  default = ""
}

# Your providers allow access to AWS and also
# define the region in use. For security it is
# best not to put your access key or secret key
# in here; instead set up awscli and configure
# them with your environment
provider "aws" {
  region = "${var.aws_region}"
  version = "~> 1.7"
}
provider "aws" {
  alias = "aws-us-east-1"

  region = "us-east-1"
  version = "~> 1.7"
}

module "certs-us-east-1" {
  source = "modules/acm"

  providers = {
    aws = "aws.aws-us-east-1"
  }

  domain_website = "${var.domain_website}"
  route53_zone_id = "${var.route53_zone_id}"
}

module "lambda-rewrite" {
  source = "modules/lambda-rewrite"

  providers = {
    aws = "aws.aws-us-east-1"
  }
}

module "website" {
  source = "modules/static-website"

  providers = {
    aws = "aws"
  }

  domain_website = "${var.domain_website}"
  route53_zone_id = "${var.route53_zone_id}"

  # Provided by other modules
  acm_arn = "${module.certs-us-east-1.acm_arn}"
  lambda_arn = "${module.lambda-rewrite.lambda_arn}"
}

